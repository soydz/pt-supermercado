plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version "8.1.0"
    id 'jacoco'
    id "org.sonarqube" version "7.2.2.6593"
}

group = 'com.soydz'
version = '0.0.1-SNAPSHOT'
description = 'pt-supermercado'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jacoco {
    toolVersion = '0.8.12'
}

repositories {
    mavenCentral()
}

configurations {
    mockitoAgent
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'
    compileOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    developmentOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    developmentOnly 'com.scalar.maven:scalar-webmvc:0.5.8'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core'
    // AÃ±ade mockito-core como agente (sin dependencias transitivas)
    mockitoAgent('org.mockito:mockito-core') { transitive = false }
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'io.rest-assured:rest-assured'
}

tasks.named('test') {
    useJUnitPlatform()

    jvmArgs "-javaagent:${configurations.mockitoAgent.asPath}" // Usa asPath para obtener la ruta absoluta del JAR

    finalizedBy jacocoTestReport

    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

test {
    filter {
        includeTestsMatching "*Test"
    }
}

tasks.register('integrationTest', Test) {
    group = 'verification'
    description = 'Run integration tests'

    //testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

integrationTest {
    filter {
        includeTestsMatching "*IT"
    }
}

check.dependsOn integrationTest

spotless {
    java {
        importOrder('java', 'javax', 'org.springframework', 'org', 'com', '')
        removeUnusedImports()
        googleJavaFormat('1.31.0')
        formatAnnotations()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

jacocoTestReport {
    dependsOn test

    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, excludes: ['**/entity/**',
                                     '**/dto/**',
                                     '**/service/exception/**',
                                     '**/*Application*',
                                     '**/config/*OpenApi*'])
    }))

    reports {
        html.required = true
        xml.required = true
    }
}

sonar {
    properties {
        property "sonar.projectKey", "soydz_pt-supermercado"
        property "sonar.organization", "soydz"
        property "sonar.coverage.jacoco.xmlReportPaths",
                "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.coverage.exclusions", ["**/entity/**", "**/dto/**", "**/service/exception/**", "**/*Application*", '**/config/*OpenApi*'].join(',')
    }
}
